package blang.mrf.hotpotts

import blang.mrf.MRFInteractor
import blang.mrf.MRFGraph
import briefj.collections.UnorderedPair

model HotPotts {
  /*
   * IMPORTANT: 
   *   laws block:      logf are unnormalized (does not include Z(beta))
   *   generate block:  an inexact, Gibbs sampler is used to generate data
   */
//  param GlobalDataSource data
//  param HPSamplerOptions samplerOptions
  param Integer numGibbsIterations ?: 2000
  param Plate<String> N
  param Integer numClasses
  param MRFGraph graph
  param MRFInteractor beta
  random Plated<IntVar> realization


  laws {
      for (UnorderedPair<String, String> edge : graph.edgeList) {
          | beta, IntVar u = realization.get(N.index(edge.getFirst())), IntVar v = realization.get(N.index(edge.getSecond()))
            ~ LogPotential(beta.logEdgePotential(u, v))
      }
  }
  
  generate(rand) {
    Generators::GibbsDiscreteMRFInPlace(rand, realization, N, beta, graph, numGibbsIterations)
  }


}