package blang.validation.internals.fixtures


model SpikeAndSlab {
  
  random List<SpikedRealVar> variables
  
  param RealVar zeroProbability
  param RealDistribution nonZeroLogDensity
  
  laws {
    for (int index : 0 ..< variables.size) {
      logf(zeroProbability, nonZeroLogDensity, RealVar variable = variables.get(index)) {
        if (zeroProbability < 0.0 || zeroProbability > 1.0) return NEGATIVE_INFINITY
        if (variable == 0.0) {
          log(zeroProbability)
        } else { 
          log(1.0 - zeroProbability) + nonZeroLogDensity.logDensity(variable)  
        }
      }
      logf(SpikedRealVar variable = variables.get(index)) {
        if (variable.selected.isBool) return 0.0
        else return NEGATIVE_INFINITY
      }
      variables.get(index) is Constrained 
    }
    
  }
  
  generate(rand) { 
    for (SpikedRealVar variable : variables) {
      (variable.selected as WritableIntVar).set(Generators::bernoulli(rand, zeroProbability).asInt)
      (variable.continuousPart as WritableRealVar).set(nonZeroLogDensity.sample(rand)) 
    }
  }
}